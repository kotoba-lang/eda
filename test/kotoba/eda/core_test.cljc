(ns kotoba.eda.core-test
  (:require [clojure.test :refer [deftest is]]
            [clojure.edn :as edn]
            [kotoba.eda.core :as eda]))

(deftest sample-flow-produces-evidence
  (let [flow (edn/read-string (slurp "docs/sample_flow.edn"))
        result (eda/run-flow flow)]
    (is (= :passed (:eda.flow/status result)))
    (is (= 6 (:eda.flow/pass-count result)))
    (is (= #{:signoff/timing-pvt :signoff/route :signoff/drc
             :signoff/lvs :signoff/spice-corner :signoff/ate-pattern}
           (set (map :eda.signoff/type (:eda.flow/results result)))))))

(deftest drc-finds-spacing-violations
  (let [result (eda/drc {:eda.job/tool :sw/klayout
                         :eda.job/operation :op/drc
                         :eda.drc/rules {:m1 {:min-width 1.0 :min-spacing 2.0}}
                         :eda.drc/shapes [{:shape/id :a :layer :m1 :rect [0 0 2 2]}
                                          {:shape/id :b :layer :m1 :rect [3 0 5 2]}]})]
    (is (= :failed (:eda.signoff/status result)))
    (is (= 1 (get-in result [:eda.signoff/metrics :violations])))))

(deftest lvs-compares-connectivity
  (let [job {:eda.job/tool :sw/netgen
             :eda.job/operation :op/lvs
             :eda.lvs/source-netlist {:eda.netlist/devices [{:type :resistor :pins {:p :a :n :b} :value 10}]}
             :eda.lvs/layout-netlist {:eda.netlist/devices [{:type :resistor :pins {:p :a :n :c} :value 10}]}}]
    (is (= :failed (:eda.signoff/status (eda/lvs job))))))
